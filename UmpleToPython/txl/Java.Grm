comments
    //
    /*  */
end comments

compounds 
    '== '!= '>= '<= '+= '-= '-- '++
end compounds 

tokens
    number  "(-\s?)?\d+(.\d+)?"
    usercode "<TXL UGM>[#(</TXL UGM>)]*</TXL UGM>"
end tokens

%--------------------%
%     Statements     %
%--------------------%

keys
 return new if else for true false while this True False + - * / ? ; : pass not and or throw
end keys

define acess_modifier
        'private
    |   'public
    |   'protected
end define

define boolean_operator
        '&&
    |   '|'|
end define

define comparator
        '==
    |   '!=
    |   '>
    |   '<
    |   '>=
    |   '<=
end define

define arithmatic_operator
        '+
    |   '-
    |   '*
    |   '/
    |   '%
end define

define program
    [repeat package_statement]  
    [repeat import_statement]
    [repeat class_declaration]
end define

define package_statement 
    'package [imported]';
end define

define import_statement 
    'import [imported]';
end define

define imported
        [id]
    |   '*
    |   [id]'. [imported]
end define

define nested_identifier
     [nestable_value] [SPOFF] [repeat attribute_access] [SPON]
end define

define attribute_access
       '. [nestable_value] 
end define

define nestable_value 
        [id] [SPOFF] [repeat nestable_extension] [SPON]
    |   'this
    |   'super
    |   [parentheses_value]
end define

define nestable_extension
        [func_call_parentheses]
    |   '< [SPON] [list id] '> [SPOFF]
    |   '< [SPON] '? 'extends [id] '> [SPOFF]
    |   '[ [SPON] [list value] '] [SPOFF]
end define 

define func_call_parentheses
    '( [SPON] [list value] ') [SPOFF]
end define

define parentheses_value
    '( [value]')
end define 
define base_value
       'null
    |   'true
    |   'false
    |   [casting]
    |   [number] [opt 'f]
    |   [stringlit]
    |   [charlit]
    |   [nested_identifier]
    |   '-- [nested_identifier]
    |   '++ [nested_identifier]
    |   [parentheses_value]
    |   [new_call]
    |  '! [value]

end define

define value
        [base_value] [opt value_continuation]
end define

define value_continuation
        [boolean_operator] [value]
    |   [arithmatic_operator] [value]
    |   [comparator] [value]
    |   [method_reference_terminal]
    |   [ternary_terminal]
    |   '++ [opt value_continuation]
    |   '-- [opt value_continuation]
    |   [value_continuation_assignment]
end define

define value_continuation_assignment
    '=  [value]
    |   '+=  [value]
    |   '-=  [value]
end define

define ternary_terminal
    '? [value] ': [value] %should be base_value?
end define

define method_reference_terminal
    ':: [id]
end define 

define new_call
    'new [nested_identifier]
end define


define stmt_return
        'return [opt value] '; [NL]
end define

define statement
        [stmt_return]
    |   [conditional_block]
    |   [for_loop]
    |   [for_in_loop]
    |   [while_loop]
    |   [double_value] [opt ';] [NL]
    |   [throw_statement]
    |   [switch_case]
    |   [try_catch]
end define

define double_value
    [value] [opt value]
end define

define while_loop
    'while( [value] ') [NL] '{ [NL] [IN] [repeat statement] [EX] '} [NL]
end define

define ternary
    [value]  '? [!] [value] ': [value]
end define

define for_loop
    'for( [double_value]'; [value]'; [value]') '{ [NL] [IN] [repeat statement] [EX] '} [NL]
end define

define for_in_loop
    'for( [double_value] ': [value]')'{ [NL] [IN] [repeat statement] [EX] '} [NL]
end define

define switch_case
    'switch( [value] ') '{ [repeat switch_case_case] [opt switch_case_default] '}
end define

define switch_case_case
    'case [value] ': [repeat statement] 'break;
end define

define switch_case_default
    'default ': [repeat statement]
end define

define conditional_block
    [if] [repeat else_if] [opt else]
end define

define try_catch 
    'try '{  [repeat statement]  '} 'catch '( [method_parameter] ') '{ [repeat statement] '}
end define

define if 
    'if '( [value] ') '{  [repeat statement]  '}
    | 'if '( [value] ') [statement]
end define

define else_if
    'else 'if '( [value] ') [NL]'{ [NL] [IN] [repeat statement] [EX] '} [NL]
end define

define else
    'else [NL]'{ [NL] [IN] [repeat statement] [EX] '} [NL]
end define

define throw_statement
    'throw 'new [id] '( [list value] ');
end define

define casting
    '( [nested_identifier]') [value]
end define

define method_parameter
    [nested_identifier] [id]
    | [var_arg]
end define

define decorator
        '@ [SPOFF] [id] '( [SPON] [list base_value][SPOFF] ') [SPON] [NL]
    |   '@ [SPOFF] [id] [SPON] [NL]
end define

define throws
    'throws [list nested_identifier]
end define

define var_arg
    [nested_identifier] [!] '... [id]
end define 

define concrete_method_declaration
        [opt decorator] [acess_modifier] [opt static] [nested_identifier] [id] '( [list method_parameter] ') [opt throws] [NL] '{ [method_content] '} [NL]
end define

define method_content
        [repeat statement]
    |   [repeat statement] [usercode] [repeat statement]
end define

define method_declaration
        [concrete_method_declaration]
    |   [abstract_method_declaration]
end define

define abstract_method_declaration
    [acess_modifier] [opt 'abstract] [nested_identifier] [id] '( [list method_parameter] '); [NL]
end define

define inheritance_statement
        'extends
    |   'implements
end define


%--------------------%
%        Class       %
%--------------------%

define class_declaration
        [concrete_class_declaration]
    |   [interface_declaration]
end define

define concrete_class_declaration
    [opt acess_modifier] [opt 'abstract] 'class [nested_identifier] [repeat inheritance_list] [NL] '{ [NL] [IN] [class_body_decl] [EX] '} [NL]
end define

define inheritance_list
    [inheritance_statement] [list nested_identifier]
end define

define interface_declaration
    [opt acess_modifier] 'interface [nested_identifier] [repeat inheritance_list] [NL] '{ [NL] [IN] [class_body_decl] [EX] '} [NL]
end define  

define static
    'static
end define

define volatile
    'volatile
end define

define transient
    'transient
end define

define constructor
    [acess_modifier] [id]'( [list method_parameter] ') [opt throws] [NL] '{ [NL] [IN] [repeat statement] [EX] '} [NL]
end define

define enum_declaration
    [opt acess_modifier] 'enum [id] '{ [list id]'}
end define

define member_variable_declaration
    [opt acess_modifier] [opt transient] [opt static] [opt final] [opt volatile] [nested_identifier] [id] [opt member_variable_assignment]';
end define

define final
    'final
end define

define member_variable_assignment
    '= [value]
end define

define class_body_decl
    [repeat class_body_element]
end define

define class_body_element
        [enum_declaration]
    |   [member_variable_declaration]
    |   [constructor]
    |   [method_declaration]
end define
