include "Java.Grm"

redefine boolean_operator
    ...
    |'and
    | 'or
end redefine

redefine comparator
    ...
    |   'is
    |   'in
end redefine

redefine nestable_value
    ...
    | 'self
    |  [array_access]
end redefine


define array_access
    [id] [SPOFF] '[ [value] '] [SPON]
end define

redefine assignment
    ...
    |   [nested_identifier] '= [value] [NL]
end redefine

redefine statement
    ...
    |  [nested_identifier] [NL]
    | 'pass
end redefine

redefine stmt_return
    ...
    |   'return [value] [NL]
end redefine


redefine value_no_recursion
    ...
    |   'True
    |   'False
    |   'None
    |   '[ [list value] ']
    |   'not [boolean_expression] 
end redefine

define python_function_name
        'tuple
    |   'len
    |   'super
end define

redefine function_call
    ...
    |   [python_function_name] [SPOFF] '( [SPON] [list value]')
end redefine


redefine for_loop
    ...
    |'for [nestable_value] 'in 'range( [value] ', [value] ', [value] '): [NL] [IN] [repeat statement] [EX] [NL]
end redefine

redefine for_in_loop
    ...
    | 'for [id] 'in  [nested_identifier] [SPOFF] ': [SPON] [NL] [IN] [repeat statement] [EX] [NL]
end redefine

redefine while_loop
    ...
    | [opt variable_declaration] 'while [value] ': [NL] [IN] [repeat statement] [EX] [NL]
end redefine

redefine if 
    ... 
    |   'if [value] ': [NL] [IN] [repeat statement] [EX] [NL]
end redefine

redefine else_if
    ...
    |   'elif [value] ': [NL] [IN] [repeat statement] [EX] [NL]
end redefine

redefine else
    ...
    |   'else:  [NL] [IN] [repeat statement] [EX] [NL]
end redefine

redefine throw_statement
    ...
    |   'raise 'RuntimeError( [stringlit] ') [NL]
end redefine


redefine method_parameter
    ...
    | [id]
end redefine

define decorator
    '@ [SPOFF] [id] [SPON] [NL]
end define

redefine concrete_method_declaration
    ...
    |   [opt decorator] 'def  [id] [SPOFF]'(self, [SPON] [list id] [SPOFF] '): [SPON] [NL] [IN] [repeat statement] [NL] [EX] [NL]
    |   [opt decorator] 'def  [id] [SPOFF]'(self): [SPON] [NL] [IN] [repeat statement] [NL] [EX] [NL]
    |   [opt decorator] 'def  [id] [SPOFF]'(): [SPON] [NL] [IN] [repeat statement] [NL] [EX] [NL]
end redefine

redefine abstract_method_declaration
    ...
    |   [SPOFF] '@abstractmethod [SPON] [NL] 'def  [id] [SPOFF]'(self, [SPON] [list id] [SPOFF] '): [SPON] [NL] [IN] [repeat statement] [NL] [EX] [NL]
    |   [SPOFF] '@abstractmethod [SPON] [NL] 'def  [id] [SPOFF]'(self): [SPON] [NL] [IN] [repeat statement] [NL] [EX] [NL]
end redefine


redefine constructor
    ...
    | 'def '__init__ [SPOFF] '(self, [SPON] [list id] [SPOFF]'): [SPON] [NL] [IN] [repeat statement] [NL] [EX] [NL]
    | 'def '__init__ [SPOFF] '(self): [SPON] [NL] [IN] [repeat statement] [NL] [EX] [NL]
end redefine

redefine concrete_class_declaration
    ... 
    | [repeat import_statement] [NL] 'class [class_name] [SPOFF] [opt inheritance_group] ': [SPON] [NL] [IN] [class_body_decl] [NL] [EX] [NL]
end redefine

define inheritance_group
    '( [SPON] [list class_name] [SPOFF] ')
end define

redefine interface_declaration
    ... 
    |  [import_statement] [NL] 'class [class_name] [SPOFF] '(ABC): [SPON] [NL] [IN] [class_body_decl] [NL] [EX] [NL]
    |  [import_statement] [repeat import_statement] [NL] 'class [class_name] [SPOFF] '(ABC, [SPON] [list class_name] [SPOFF] '): [SPON] [NL] [IN] [class_body_decl] [NL] [EX] [NL]
end redefine

redefine class_body_decl
    ...
    | 'pass
    | [opt constructor] [repeat method_declaration]
end redefine

redefine variable_declaration
    ...
    |   [assignment] [FL]
    |   [id] [FL]
end redefine

redefine import_statement
    ...
    |   'from [class_name] 'import [class_name] [NL]
    |   'from [class_name] 'import [list class_name] [NL]
end redefine 