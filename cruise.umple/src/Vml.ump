/*

Copyright: All contributers to the Umple Project

This file is made available subject to the open source license found at:
http://umple.org/license

Model for the variability modeling language, used to create product lines that
have various features. This is an add-on capability for Umple
*/

namespace cruise.vml;

/*
 * VML (Variability Modeling Language) is an Umple add-on that
 * allows you co construct and Umple model from a variety of
 * CodeSnippets that are put together to create different
 * versions of a product line or product family, by invoking
 * one or more Variants of a variationPoint within a concern
 * An online tool is available at
 * http://try.umple.org/vml.php
 * It is the intention to integrate VML into the core Umple language
 */
class VmlSystem
{
  code = null;
  
  0..1 -> * CodeSnippet;
  0..1 -- * Concern;
  
  before getCode 
  {
    code = "";
    for (CodeSnippet snippet : codeSnippets)
    {
      code += snippet.getCode();
      code += "\n";
    }
  }
  
}

/*
 * A named group of VariationPoints.
 */
class Concern
{
  name;
  
  key { name }
}

/*
 * Identifies where one product line member may differ
 * from another. 
 */
class VariationPoint
{
  name;
  kind { Optional, Alternative }
  0..1 -> 0..1 CodeSnippet;
  0..1 -- * Variant;
  * -- 0..1 Concern concern;
  * -> * VariationPoint requires;
  
  key { concern, name, kind, codeSnippet }
}

/* 
 * A named code snippet within a variant
 */
class Variant
{
  name;
  0..1 -> 0..1 CodeSnippet;
  key { name, codeSnippet }
}

/*
 * A piece of code (typically umple) that will be
 * selected to be added to a product line if
 * a certain variant within a variation point is
 * selected.
 */
class CodeSnippet
{
  code;
  
  key {code}
}

/* 
 * Specialist parser for parsing VML
 */
class VmlParser
{
  depend cruise.umple.compiler.Parser;
  depend cruise.umple.parser.Token;
  depend cruise.umple.parser.ParseResult;

  isA Parser;
  
  0..1 -> 0..1 VmlSystem system;
}

use Vml_Code.ump;
