// UML state machine diagram of a traffic light system, written in Umple
class IntersectionTimerDriven {
  
  // Traffic lights are green for 25s in one direction
  // and 20s in the other. 
  Integer directionOneGoTime=25000;
  Integer directionTwoGoTime=20000;

  // Lights are yellow for 5s
  Integer yellowTime=5000;

  // Pedestrians have 10s extra warning
  Integer extraPedestrianWarning=10000;  
  
  flow {
    FourWayStop 
    {
      entry / {
        directionOneLight = DirectionOneLight.FlashingRed;
        directionTwoLight = directionTwoLight.FlashingRed;
      }
      initiateRegularOperation -> DirectionOneGo;    
    }
    DirectionOneGo
    {
      entry / {
        directionOneLight = directionOneLight.Green;
        directionTwoLight = directionTwoLight.Red;
      }
      after(directionOneGoTime) -> DirectionOneHalting;
      detectMalfunction -> FourWayStop;
    }
    DirectionOneHalting
    {
      entry / {directionOneLight = directionOneLight.Yellow; }
      after(yellowTime) -> DirectionTwoGo;
      detectMalfunction -> FourWayStop;
    }
    DirectionTwoGo
    {
      entry / {
        directionOneLight = directionOneLight.Red;
        directionTwoLight = directionTwoLight.Green;
      }
      after(directionOneGoTime) -> DirectionTwoHalting;
      detectMalfunction -> FourWayStop;
    }
    DirectionTwoHalting 
    {
      entry / {directionTwoLight = directionTwoLight.Yellow; }
      after(yellowTime) -> DirectionOneGo;
      detectMalfunction -> FourWayStop;
    }        
  }
  
  pedestrianFlow
  {
    FourWayStop
    {
      entry / {P1Light = P1Light.DoNotWalk;  P2Light = P2Light.DoNotWalk;}
      initiateRegularOperation -> P2Go;
    }
    P1Go
    {
      entry / {P1Light = P1Light.Walk;}
      after(directionOneGoTime-extraPedestrianWarning)-> P1Halting;
      detectMalfunction -> FourWayStop;
    }
    P1Halting
    {
      entry / {P1Light = P1Light.Flashing;}
      after(extraPedestrianWarning+yellowTime) -> P2Go;
      detectMalfunction -> FourWayStop;
    }
    P2Go
    {
      entry / {P2Light = P2Light.Walk;}
      after(directionTwoGoTime-extraPedestrianWarning) -> P2Halting;
      detectMalfunction -> FourWayStop;
    }
    P2Halting
    {
      entry / {P2Light = P2Light.Flashing;}
      after(extraPedestrianWarning+yellowTime)  -> P1Go;
      detectMalfunction -> FourWayStop;
    }
  }
  
  
  directionOneLight 
  {
    FlashingRed {} 
    Red {}
    Yellow {}
    Green {}    
  }
  
  directionTwoLight
  {
    FlashingRed {}
    Red {}
    Yellow {}
    Green {}
  }
  
  P1Light
  {
    DoNotWalk {}
    Flashing {}
    Walk {}
  }
  P2Light
  {
    DoNotWalk {}
    Flashing {}
    Walk {}
  }
}
